This tutorial describes a technique for defining a limited form of '''constraint domain''' in Twelf, that is, a type with non-trivial equations among its terms.  Unlike Twelf's built-in (but [[Constraint_domains_and_coverage_checking|currently problematic]]) constraint domains of rationals, integers, and strings, these constraint domains are already part of the LF type theory and thus interact nicely with each other and with the rest of the theory.  And, as we shall see, they can be used for many of the same purposes, not to mention a whole host of new ones, including a limited form of type refinement.

Although this tutorial has (user-defined) constraint domains as its main theme, it touches on a lot of other topics and techniques, and so has some value as a general tutorial on Twelf.

== Motivation ==

We begin with some motivation (and review), which you can skip if you are interested in getting right to the examples that follow.

LF is a representational framework:  given a particular formal system, we construct LF signatures that determine sets of terms (and types and kinds) that can stand as representatives of the constituents of the formal system.  We do this in such a way that the syntactic and proof-theoretic structures of the formal system, for example the binding structure of terms or the inductive construction of proofs, are mirrored in the type-theoretic structure of LF.  The measure of success for such a representation is what we call [[adequacy]].

Equality of terms (and types and kinds) clearly plays a key role here, since adequacy depends on what terms are considered equal.  Twelf's built-in notion of equality, which is much coarser than plain syntactic identity, is the combination of [[beta-equivalence|beta]]- and [[eta-equivalence]].  The former, along with the notion of [[higher-order abstract syntax]], is what allows us to represent object-level substitution using meta-level function application.  And the latter implies a kind of extensionality or behavioral equivalence that gets rid of some annoying duplication in our representations that would otherwise make adequacy much more difficult to achieve.  Beta-eta equivalence has the very nice property that its equivalence classes have unique [[canonical form]]s, which are inductively defined; it is these canonical forms that are used to represent object-level entities.  Equality also plays a fundamental role in the [[Logic programming|operational semantics]] of Twelf, since one of its main components, unification, is all about finding substitutions that make a set of terms equal up to beta-eta equivalence, which is a crucial but quite non-trivial endeavor.

However, despite the importance of equality, it remains an ''internal'' notion: there are no built-in "equality types" that we can use to reason about equality explicitly, for example to prove uniqueness of constructed objects or functionality of relations.  Instead, we must [[equality|define]] equality for each type where it is needed, and prove [[respects_lemma | respects]] and [[uniqueness_lemma | uniqueness]] lemmas in order to make use of it.  This approach has the advantage, though, that the same or closely-related techniques will work when the equality we define is ''not'' the built-in equality but one that carries more information.

Another crucial aspect of LF from a representational standpoint is that its terms are generated ''freely'' from its signature.  That is, no terms are present that are not built from the constructors in the signature and the variables in the context, and no identifications between terms are made, except those that follow from beta-eta equivalence.  Without this, it would be difficult to represent aspects of our formal systems that are defined (freely) by induction.  On the other hand, this freeness can also be an impediment, as when what we really want is a kind of ''algebra'':  a set endowed with certain operations that satisfy (non-trivial) equations.  Think, for example, of the type <tt>bool</tt>, which is freely generated by its two constants, <tt>true</tt> and <tt>false</tt>, but which has operations <tt>and</tt>, <tt>or</tt>, <tt>not</tt>, <tt>implies</tt>, etc., that satisfy the equations of Boolean Algebra.

We are therefore led to wonder whether we can represent such "algebras" as LF types.

== Example 1: Even and Odd ==

We borrow our first example from the tutorial on [[Output factoring]]:

<twelf discard="true">

nat : type.
z : nat.
s : nat -> nat.

odd : nat -> type.
even : nat -> type.

z-e : even z.
s-o : odd (s X) <- even X.
s-e : even (s X) <- odd X.

</twelf>

Here, <tt>even</tt> and <tt>odd</tt> are ''predicates'' on <tt>nat</tt> and are defined in terms of their relation to <tt>s</tt>.  The [[output factoring|output factoring tutorial]] shows you how to prove that, under this definition, every natural number is either even or odd; the statement of the theorem requires a disjoint-union type, and the proof requires a lemma to take care of an output coverage problem.

=== An approach using indexing ===

Since the subsets of even and odd natural numbers are disjoint, you might think of another approach to representing these subsets, namely, by introducing a new type of ''natural number sorts'', and indexing <tt>nat</tt> by the sort:

<twelf discard="true">
nat_sort : type.
even : nat_sort.
odd : nat_sort.

nat : nat_sort -> type.
z : nat even.
s : nat even -> nat odd.    % Doesn't work:
s : nat odd -> nat even.    % redefinition of s
</twelf>

We'd like for <tt>s</tt> to have both of the declared types at the same time, but that is not possible in LF (though some proposed extensions of LF to include ''refinement types'' do allow something like this).  Alternatively, we might try to write something like

<twelf discard="true">
s : nat X -> nat (f X).
</twelf>

for some term <tt>f</tt> of type <tt>nat_sort -> nat_sort</tt> with the property that <tt>f even</tt> = <tt>odd</tt> and <tt>f odd</tt> = <tt>even</tt>, but again this is not possible in LF, since terms are freely generated and functional terms are parametric in their arguments (i.e., there are no destructors or case-splitting on arguments).  This approach seems to be stuck.  Or is it?

=== A constraint domain of natural-number sorts ===

The idea is to use a higher-order "Church-encoding" for our two-element type and to use abbreviations to "hide" the implementation of this type:

<twelf import="start" export="ex1">

t : type.

%abbrev nat_sort : type = t -> t -> t.
%abbrev even : nat_sort = [even:t] [odd:t] even.
%abbrev odd  : nat_sort = [even:t] [odd:t] odd.

%abbrev add1-mod2 : nat_sort -> nat_sort = [s:nat_sort] [even:t] [odd:t] s odd even.

nat : nat_sort -> type.
z : nat even.
s : nat X -> nat (add1-mod2 X).

</twelf>

Why does this work?  As long as the type <tt>t</tt> does not become [[subordination|subordinate]] to any other type, the only canonical terms of type <tt>nat_sort</tt> are <tt>even</tt> and <tt>odd</tt>, and so the type looks "from the outside" just like the type <tt>nat_sort</tt> from the previous section.  However, <tt>add1-mod2</tt>, rather than being a constructor on this type that freely generates new terms, is instead a "pre-existing" canonical term of type <tt>nat_sort -> nat_sort</tt>.  In fact, there are ''four'' canonical terms of this type, the other three being

<twelf import="ex1">

%abbrev const_even : nat_sort -> nat_sort = [s:nat_sort] [even:t] [odd:t] s even even.   % or: [s] even
%abbrev const_odd  : nat_sort -> nat_sort = [s:nat_sort] [even:t] [odd:t] s odd  odd.    % or: [s] odd
%abbrev identity   : nat_sort -> nat_sort = [s:nat_sort] [even:t] [odd:t] s even odd.    % or: [s] s

</twelf>

Similarly, there are (2<sup>2</sup>)<sup>2</sup> = 16 canonical terms of type <tt>nat_sort -> nat_sort -> nat_sort</tt>, and 2<sup>4</sup> = 16 canonical terms of type <tt>(nat_sort -> nat_sort) -> nat_sort</tt>, and so on.  In fact, what we have here are all lambda-definable functions in the full type-hierarchy over {<tt>even</tt>, <tt>odd</tt>}, which, since this set is finite, is the same as the full (set-theoretic) type-hierarchy over {<tt>even</tt>, <tt>odd</tt>}.  And these functions are just there waiting to be used!

To see one such use, let's define the predicate <tt>plus X Y Z</tt>.  What should be its type?  We'll give two definitions, the first with minimal type information:

<twelf import="ex1">

plus_min : nat X -> nat Y -> nat Z -> type.
%mode plus_min +N1 +N2 -N3.

plus_min_z : plus_min N1 z N1.
plus_min_s : plus_min N1 (s N2) (s N3) <- plus_min N1 N2 N3.

%worlds () (plus_min N1 N2 N3).
%total N2 (plus_min N1 N2 N3).

</twelf>

This works fine, but we can do better.  We know, for example, that if <tt>X</tt> and <tt>Y</tt> are the same, then <tt>Z</tt> will be <tt>even</tt>, and if <tt>X</tt> and <tt>Y</tt> are different, then <tt>Z</tt> will be <tt>odd</tt>.  In other words, tracking the sorts of the elements involved in an addition requires being able to compute addition mod 2.  But this is just a (finite) function in the type-hierarchy over {<tt>even</tt>, <tt>odd</tt>}, and so to get a maximally precise type for <tt>plus</tt>, we can just define the term of type <tt>nat_sort -> nat_sort -> nat_sort</tt> that represents addition mod 2, and then use that in the type:

<twelf import="ex1">

%abbrev plus-mod2 : nat_sort -> nat_sort -> nat_sort
   = [s1:nat_sort] [s2:nat_sort] [even:t] [odd:t] s1 (s2 even odd) (s2 odd even).

plus : nat X -> nat Y -> nat (plus-mod2 X Y) -> type.
%mode plus +N1 +N2 -N3.

plus_z : plus N1 z N1.
plus_s : plus N1 (s N2) (s N3) <- plus N1 N2 N3.

%worlds () (plus N1 N2 N3).
%total N2 (plus N1 N2 N3).

</twelf>

<twelflink import="ex1" check="true">See Twelf's output</twelflink>.  You'll see in this output that all of our abbreviations have been expanded, which makes the output a bit harder to read.  It would have been better to have used definitions rather than abbreviations, but (at present anyway) this is, for technical reasons, incompatible with Twelf's meta-checks.

Also notice that, apart from the change in name from <tt>plus_min</tt> to <tt>plus</tt>, the declarations of <tt>plus_z</tt> and <tt>plus_s</tt> are the same; it's just the type that has become more precise.  As an exercise, you may like to check that if <tt>f</tt> is any of the other 15 possible canonical forms of type <tt>nat_sort -> nat_sort</tt>, and you replace the declaration of <tt>plus</tt> above with

<twelf discard=true>
plus : nat X -> nat Y -> nat (f X Y) -> type.
</twelf>

then at least one of the two declarations for <tt>plus_z</tt> or <tt>plus_s</tt> will no longer type-check without generating unresolved constraints.  Finally, we observe that, as compared to the definitions of even and odd in the tutorial on [[output factoring]], it is intrinsic in our encoding via the constraint domain <tt>nat_sort</tt> that every natural number is either even or odd, and so this is not something that needs to be proved.

== Example 2: Polarities ==

For our next example, we are going to use the same constraint domain that we used in Example 1, and for a similar purpose (although we will be renaming it to suit the current situation).  Suppose that we have a language with a type system that includes a function-space operator, <tt>=></tt>, along possibly with some others, such as products, sums, and type constants, and we have represented these as canonical forms of an LF type <tt>tp</tt>.  We want to add an operator <tt>mu</tt> to represent ''positive inductive types''.  What this means is that if we have a type expression <tt>F : tp -> tp</tt> such that for any <tt>x : tp</tt>, the type <tt>F x</tt> only has ''positive'' occurrences of <tt>x</tt> (that is, every occurrence of <tt>x</tt> is on the left-hand side of an even number of <tt>=></tt> operators), then we want to be able to form <tt>mu F : tp</tt>.  Furthermore, these are the ''only'' <tt>mu</tt>-types that we want to form; i.e., we want this to be an [[Intrinsic and extrinsic encodings|intrinsic encoding]].  All of these types will have introduction and elimination forms, but for the purposes of this example, we will just focus on the types themselves.

=== An approach via predicates ===

A natural approach to this problem is to try to represent the positivity condition as a predicate <tt>pos</tt> on <tt>tp -> tp</tt> and to define this predicate mutually with another predicate <tt>neg</tt> on <tt>tp -> tp</tt>, as follows:

<twelf import="start" export="ex2a">
tp : type.

pos : (tp -> tp) -> type.
neg : (tp -> tp) -> type.

un : tp.                                       % unit type
*  : tp -> tp -> tp.    %infix right 11 *.     % product type
=> : tp -> tp -> tp.    %infix right 10 =>.    % function type
mu : {F:tp -> tp} pos F -> tp.                 % inductive type

pos_id : pos ([a] a).

pos_un : pos ([a] un).
pos_*  : pos ([a] F1 a) -> pos ([a] F2 a) -> pos ([a] F1 a * F2 a).
pos_=> : neg ([a] F1 a) -> pos ([a] F2 a) -> pos ([a] F1 a => F2 a).
pos_mu : ({b:tp} pos ([a] b) -> pos ([a] F a b)) -> pos ([a] mu ([b] F a b) _).

neg_un : neg ([a] un).
neg_*  : neg ([a] F1 a) -> neg ([a] F2 a) -> neg ([a] F1 a * F2 a).
neg_=> : pos ([a] F1 a) -> neg ([a] F2 a) -> neg ([a] F1 a => F2 a).
neg_mu : ({b:tp} neg ([a] b) -> neg ([a] F a b)) -> neg ([a] mu ([b] F a b) _).
</twelf>
Some comments about this code are in order:

* We have chosen to the make the type constructor <tt>F</tt> an [[explicit parameter]] of <tt>mu</tt> in order to make <tt>mu</tt>-terms appear more natural, with the positivity proof relegated to second position.  However, we could just as easily have left this parameter implicit, declaring <tt>mu : pos F -> tp</tt>, since we would still have access to <tt>F</tt> through unification with terms involving type ascriptions, as in <tt>mu (P : pos F)</tt>.

* We define <tt>pos F</tt> and <tt>neg F</tt> by induction on the body of <tt>F</tt>, taking advantage of the inductive characterization of LF canonical forms of type <tt>tp -> tp</tt> as the canonical forms of type <tt>tp</tt> in a context that includes an extra assumption <tt>a : tp</tt>.  The cases for <tt>mu</tt> say that a type <tt>a</tt> occurs positively (or negatively) in a <tt>mu</tt>-type if it occurs positively (or negatively) in its body, and are necessary if we want to allow nested <tt>mu</tt>-types (but see below!).  The proof components of the <tt>mu</tt>-type are not used, hence the underscores <tt>_</tt> in these declarations.

* We have chosen to define <tt>pos</tt> and <tt>neg</tt> using types involving the <tt>-></tt> arrow rather than the <tt>&lt;-</tt> arrow that is typical in logic-programming-style definitions, e.g.,
<pre style="border:0px; background-color:white">
    pos_* : pos ([a] F1 a * F2 a)
              <- pos ([a] F1 a)
              <- pos ([a] F2 a).
</pre>
: There are two reasons.  First, these declarations are not the same:  using the earlier declaration, a term of the form <tt>pos_* P1 P2 : pos ([a] F1 a * F2 a)</tt> has <tt>P1</tt> as the positivity proof for <tt>F1</tt> and <tt>P2</tt> as the positivity proof for <tt>F2</tt>, whereas in the declaration just given, it is reversed!  Second, we prefer to use <tt>-></tt> when we are representing data (i.e., making object-level definitions) and <tt>&lt;-</tt> when we are representing programs (i.e., proving metatheorems), but this is a matter of style.

All of this appears to work, and we were able, in passing, to make some useful comments about the code, but unfortunately there is a problem here, and the problem has to do with nested <tt>mu</tt>-types.  As a simple example, take the μ-type μ<tt>X</tt>. μ<tt>Y</tt>. <tt>X * Y</tt> (which could be a considered the type of infinite streams of infinite streams of infinite streams of ...).  In order for the outer <tt>mu</tt> in any representation of this type to be well-formed, it's body, the inner <tt>mu</tt>, must have only positive occurrences of <tt>X</tt> in its body (which it seems to have) and similarly for the positivity of <tt>Y</tt> in the body of the inner <tt>mu</tt>, which is <tt>X * Y</tt>.  However, you can see in the clause for <tt>pos_*</tt> above that, for <tt>Y</tt> to be positive in <tt>X * Y</tt>, not only does it have to be positive in <tt>Y</tt>, which it is by <tt>pos_id</tt>, but it also has to be positive in <tt>X</tt>, which we have not arranged to be the case.

Now, since every occurrence of a variable is both positive and negative in an expression in which it does not occur&mdash;vacuously&mdash;we could add two [[catch-all case]]s to our declarations to fix this problem:

<twelf import="ex2a">
pos_vac : pos ([a] F).
neg_vac : neg ([a] F).
</twelf>

We could then represent our μ-type μ<tt>X</tt>. μ<tt>Y</tt>. <tt>X * Y</tt> with the term

<twelf import="ex2a">
streams : tp = mu ([x] mu ([y] x * y) (pos_* pos_vac pos_id)) (pos_mu ([y][py] pos_* pos_id py)).
</twelf>

However, as mentioned in the [[catch-all case|tutorial on catch-all cases]], this creates ''non-determinism'', since it is now possible to have multiple proofs of positivity for the same type constructor.  For example, we have:
<twelf import="ex2a">
proof1 : pos ([a] un * un) = pos_* pos_un  pos_un.
proof2 : pos ([a] un * un) = pos_* pos_vac pos_un.
proof3 : pos ([a] un * un) = pos_* pos_un  pos_vac.
proof4 : pos ([a] un * un) = pos_* pos_vac pos_vac.
proof5 : pos ([a] un * un) = pos_vac.
</twelf>
and so, filling in the hole labeled <tt>%{proof}%</tt> in the term
<twelf discard="true">
mu ([x] un * un) ( %{proof}% )
</twelf>
with each of the five proofs above, we would get five different canonical terms of type <tt>tp</tt> representing the same μ-type, μ<tt>X</tt>. <tt>1 * 1</tt> ! This destroys the [[adequacy]] of our representation, and is why it's best to avoid catch-all cases in object representations.  On the other hand, the same can't be said, in general, of [[metatheorem]] representation, since there we are usually more concerned about the ''existence'' of proofs rather than their unique representation (whatever that means: if you really figure out what it means for two proofs to be "equal", let me know!)  Finally, just as we mentioned in connection with Example 1 that some proposed extensions to LF to include ''refinement types'' could give a nice solution to the representation of even and odd natural numbers (among many others such "subsorts"), we should also mention here that some proposed extensions to LF to include ''proof-irrelevance'' could address the problem we have described above, by identifying all elements, or "proofs", of certain types, restoring adequacy of such representations.

<twelflink import="ex2a" check="true">See Twelf's output</twelflink> for the code above.

=== Is there an adequate intrinsic encoding? ===

So how can we achieve an adequate intrinsic encoding of μ-types?  The situation is worse than you might think:  there doesn't seem to be any hope of getting such an encoding.  We've seen that adding a catch-all case destroys adequacy, but yet we've also seen that, in representing the μ-type μ<tt>X</tt>. μ<tt>Y</tt>. <tt>X * Y</tt>, we need a positivity assumption about <tt>X</tt> in order to show that <tt>Y</tt> occurs positively in <tt>X * Y</tt>.  So our only hope seems to be to add it to the argument of <tt>mu</tt>, thus:

<twelf discard="true">
mu : {F:{a:tp} pos ([b] a) -> tp} pos F -> tp.
</twelf>

But this is not well-typed unless we also either

# change the type of <tt>pos</tt> so that it accepts arguments of the same type as <tt>F</tt>, or
# extract the underlying constructor of type <tt>tp -> tp</tt> from <tt>F</tt> and use that instead as the argument to <tt>pos</tt>.

But neither of these is possible:  we can't do 1 because <tt>pos</tt> would then have to be declared as

<twelf discard="true">
pos : ({a:tp} pos ([b] a) -> tp) -> type
</twelf>

which is circular, and we can't do 2, because to get a <tt>tp</tt> output from <tt>F</tt>, we'd need first to construct something of type <tt>pos ([b] a)</tt>, which is exactly the problem we were trying to solve in the first place.  So, it seems that our search for an adequate intrinsic encoding of μ-types is doomed.  Or is it?

=== Using the even/odd constraint domain ===

You may remember our original characterization of what it means for a type expression <tt>F : tp -> tp</tt> to have only positive occurrences of its bound variable, namely, that every occurrence of the variable is on the left-hand side of an ''even'' number of <tt>=></tt> operators.  This suggests that, in the same way that we used the <tt>nat_sort</tt> constraint domain in Example 1 as an index to <tt>nat</tt> in order to capture the even (and odd) natural numbers, we can also use it here as an index to <tt>tp</tt> to capture occurrences on the left of an even (and odd) number of <tt>=></tt>s.  When we follow this idea to its logical conclusion, this is what we get:

<twelf import="start" export="ex2b">
t : type.

%abbrev pol : type = t -> t -> t.                              % polarities
%abbrev opp : pol -> pol = [p:pol] [pos:t] [neg:t] p neg pos.  % the opposite polarity

tp : pol -> type.

un : tp P.                                                     % unit type
*  : tp P -> tp P -> tp P.            %infix right 11 *.       % product type
=> : tp (opp P) -> tp P -> tp P.      %infix right 10 =>.      % function type
mu : (tp P -> tp P) -> tp P.                                   % inductive type
</twelf>

That's it!  You may find it hard to believe that this works, so let's look at it more closely.  The constraint domain is the same one we used before, with <tt>nat_sort</tt> renamed to <tt>pol</tt> and <tt>add1-mod2</tt> renamed to <tt>opp</tt>.  In all of the type constructors the polarity index is implicitly quantified, so each will apply to both polarities, but the individual polarities themselves have no meaning&mdash;there isn't a "positive" polarity or a "negative" polarity, for example&mdash;so we haven't given them names.  The main force of the polarities is to be found in the rules for <tt>=></tt> and <tt>mu</tt>.  In the case of <tt>=></tt>, the rule says that, whatever polarity is to be assigned to the type <tt>A => B</tt>, the same polarity must be assigned to <tt>B</tt> and the opposite polarity must be assigned to <tt>A</tt>.  And in the case of <tt>mu</tt>, the fact that the functional argument to <tt>mu</tt> has domain and codomain types with the ''same'' polarity means that, if we consider this functional argument to be an abstract syntax tree with instances of the bound variable at the leaves, then an even number of applications of <tt>opp</tt> must occur between the root of this tree and any occurrence of the bound variable at the leaves.

In short, you can think of the rules as giving conditions for a particular simultaneous polarity assignment to all of the subterms of a given term to be ''coherent'', and you'll notice that if one polarity assignment is coherent, then so is its opposite, reinforcing the idea that the individual polarities are meaningless.  (We might say, following Einstein, that positivity and negativity are not ''absolute'' notions, but are only meaningful relative to other instances of one another!)  Thus, since each of the type constructors relates the polarity of its "output" to the polarities of its "inputs" (with, in the case of <tt>mu</tt>, an assumption about the polarity of the bound type variable), a given term representing one of our types will have, overall, either zero or exactly two polarity assignments for its subterms that can be coherent, with only the latter case corresponding to the type being well-formed.  It follows that the product type <tt>{P:pol} tp P</tt> adequately represents our informal notion of positive inductive types.

Here are some sample encodings of μ-types, starting with the two examples we discussed before:

<twelf import="ex2b">
%abbrev pos_ind : type = {P:pol} tp P.

streams : pos_ind = [P] mu [x] mu [y] x * y.
ones    : pos_ind = [P] mu [x] un * un.

ex1 : pos_ind = [P] mu [x] un => x.
ex2 : pos_ind = [P] mu [x] (x => un) => x.
ex3 : pos_ind = [P] mu [x] (x * x => un) => x * x.
ex4 : pos_ind = [P] mu [x] (mu [y] x => y) => x.
</twelf>

<twelflink import="ex2b" check="true">See Twelf's output</twelflink>.

You might also like to check that each of these definitions fails to type-check:

<twelf discard="true>
ex5 : pos_ind = [P] mu [x] x => un.
ex6 : pos_ind = [P] mu [x] x => x.
ex7 : pos_ind = [P] mu [x] x * (mu [y] x => y).
</twelf>

Curiously, if you remove the type ascriptions and polarity abstractions from these three ill-formed <tt>mu</tt>-types, then they ''do'' type-check, but only because Twelf, during type-reconstruction, abstracts a variable <tt>X</tt> of type <tt>t</tt> from these definitions and then assigns the "constant polarity" <tt>[t1][t2] X</tt> (which is its own opposite!) to the contradictory subterms.  This illustrates again the reliance of this method on the fact that the basic type <tt>t</tt> cannot have any canonical elements.

== Example 3: Uniform syntactic categories ==

Looking over the examples from the previous two sections, you may have noticed a pattern in the method we used to achieve simplification in each case, a method that we might call ''intrinsification by indexing''.  That is, we were given a type <tt>A</tt> with some unary predicates <tt>P</tt> on that type and we "intrinsified" those predicates by turning <tt>A</tt> into a type family <tt>A'</tt> that was indexed by a particular constraint domain whose elements represented those predicates; for any predicate <tt>P</tt>, the elements of <tt>A' P</tt> were exactly the original elements of <tt>A</tt> that satisfied <tt>P</tt>.  This opened up the possibility of giving more precise types to functions defined on <tt>A</tt> that respected the predicates, since we were are able to "track" their behavior on the indices by using definable functions in the type-hierarchy over the constraint domain.

Thus, in Example 1, the type <tt>A</tt> was <tt>nat</tt> and the predicates were <tt>even</tt> and <tt>odd</tt>.  These predicates became elements of a constraint domain <tt>nat_sort</tt> that we used to index <tt>A</tt>, and the functions <tt>s</tt> and (via its graph) <tt>plus</tt> could be tracked by functions <tt>add1-mod2</tt> and <tt>plus-mod2</tt> on <tt>nat_sort</tt>.  And in Example 2, the type <tt>A</tt> was <tt>tp -> tp</tt> and the predicates were <tt>pos</tt> and <tt>neg</tt>.  These predicates became elements of a constraint domain <tt>pol</tt> and the various type constructors were tracked with the help of (the identity function and) the function <tt>opp</tt> on <tt>pol</tt>.

We will continue to explore this theme by using a couple of examples from the typed lambda calculus and LF itself.  But first, let's look a little more carefully at the conditions under which we can expect the methods of the previous two sections to work.  Clearly, if we want there to be a bijection between <tt>A</tt> and the collection of types <tt>A' P</tt> indexed by the predicates <tt>P</tt>, then the extensions of these predicates must form a partition of <tt>A</tt> (i.e., they must be pairwise disjoint and collectively exhaustive).  This may seem like a severe restriction, but there are some ways around it, as we will see shortly.  But another restriction of the method is that, if we want to give more informative types for functions defined on <tt>A</tt>, these must must not only respect the predicates, but the induced functions on the indices must also be ''uniform'' in the sense that they can be defined parametrically on the index.  These restrictions will become clearer as we look at more examples.

=== Simply-typed lambda calculus with values ===

Our next example won't actually require a constraint domain, but it nevertheless illustrates the intrinsification-by-indexing theme we are developing, and it shows how you can sometimes get around some of the limitations of the method that were discussed at the end of the previous section.  Suppose we want to represent a simply-typed call-by-value lambda-calculus with a (big-step) evaluation relation that relates arbitrary terms with their values under evaluation, and we decide to represent typed terms intrinsically and values extrinsically:

<twelf import="start" export="ex3a">
tp : type.
=> : tp -> tp -> tp.   %infix right 10 =>.

exp : tp -> type.
lam : (exp A -> exp B) -> exp (A => B).
app : exp (A => B) -> exp A -> exp B.

val : exp A -> type.
lam-val : val (lam [x] E x).

eval : exp A -> exp A -> type.
eval-lam : eval (lam [x] E x) (lam [x] E x).
eval-app : eval (app E1 E2) V
            <- eval E1 (lam [x] E1' x)
            <- eval E2 V2
            <- eval (E1' V2) V.
</twelf>

That is, the encoding of typed terms here is intrinsic in that we are only representing well-typed terms (along with their types), and not separate types and (not necessarily well-typed) terms that are related by a typing judgment.  And our encoding of values is extrinisic in that we are representing values as a subset of terms through a separate judgment, instead of similarly building the notion into the terms.  (See the tutorial on [[Intrinsic and extrinsic encodings]], which uses some similar examples.)

<twelflink import="ex3a" check="true">See Twelf's output</twelflink> for the above declarations.

Now, we'd like to "intrinsify" the representation of values in this encoding, but there is an obstacle here that wasn't present in our previous examples:  we have two predicates here corresponding to values and general terms, but they aren't disjoint:  every values is a general term.  The solution is to introduce a separate predicate for ''non-values'', which, along with values, partition the set of terms.  Fortunately, this is possible here, since non-values have a simple inductive definition:  they are precisely the applications.  Here is the final result:

<twelf import="start" export="ex3b">
tp : type.
=> : tp -> tp -> tp.  %infix right 10 =>.

stlc_sort : type.
val : stlc_sort.
non-val : stlc_sort.

exp : stlc_sort -> tp -> type.

lam : (exp val A -> exp S B) -> exp val (A => B).
app : exp S1 (A => B) -> exp S2 A -> exp non-val B.

eval : exp S A -> exp val A -> type.
ev-lam : eval (lam [x] E x) (lam [x] E x).
ev-app : eval (app E1 E2) V
          <- eval E1 (lam [x] E1' x)
          <- eval E2 V2
          <- eval (E1' V2) V.
</twelf>

Now we can see why a constraint domain wasn't required in this case:  there are no special functions needed to track <tt>lam</tt> and <tt>app</tt> on the indices.  Yet we still get an intrinsic encoding with more informative types for <tt>lam</tt>, <tt>app</tt>, and <tt>eval</tt>.  And whereas with the extrinisic encoding we would have to prove a metatheorem that if <tt>eval E V</tt> then <tt>val V</tt>, with the intrinsic encoding this comes for free.

<twelflink import="ex3b" check="true">See Twelf's output</twelflink>.

=== The syntax of Canonical LF ===

In modern presentations of the LF type theory (see the glossary entry for [[canonical form]]s for more information), the set of syntactic objects is divided into five categories:  kinds, canonical types, atomic type families, canonical terms, and atomic terms, often denoted by the letters K, A, P, M, and R.  This stratification of syntax necessitates a similar stratification of the typing judgment (or, as we will call it, the classification judgment), the [[hereditary substitution]] judgment, and the statements of theorems about these judgments, for example the substitution theorem.

In this section, we will show how it is possible to define a constraint domain <tt>cat</tt> of ''(syntactic) categories'' to allow an economical presentation of the syntax, judgments, and metatheorems of LF.  In particular, we will have one type family of terms, one classification judgment, one substitution judgment, and one substitution theorem statement, each of which is indexed by a syntactic category.  In order to achieve this unification, we will have to make use of some functions in the type hierarchy over <tt>cat</tt>, much the way we did with <tt>add1-mod2</tt>, <tt>plus-mod2</tt>, and <tt>opp</tt> above.  To simplify the presentation for this tutorial, we will not be representing signatures, (type family or term) constants, subordination, the simple-type subscripts to hereditary substitution, or the bi-directionality of the typing judgments and substitution theorem.

==== The constrain domain of syntactic categories ====

We first define the constraint domain, <tt>cat</tt>.  There are two subtleties here.  First, because our unified classification judgment relates a syntactic object to its classifier, it will be necessary for the purpose of uniformity to introduce a category to classify kinds; we call it "unit" (letter: U) and give it a unique element <tt>*</tt> (this element might be called a "hyperkind").  Second, the hereditary substitution operation, when applied to an atomic term ''R'', can produce either a canonical term or an atomic term, depending on whether the substitution variable occurs at the head of ''R'' or not.  Consequently, we introduce a syntactic category to represent the disjoint union of canonical and atomic terms (letter: M+R), and we introduce two injections from M and R into M+R as term constructors.

Here is the definition of the constraint domain:

<twelf import="start" export="ex3d">
t : type.

%abbrev cat : type = t -> t -> t -> t -> t -> t -> t -> t.  % seven elt type

%abbrev k   : cat = [k][a][p][m][r][m+r][u] k.
%abbrev a   : cat = [k][a][p][m][r][m+r][u] a.
%abbrev p   : cat = [k][a][p][m][r][m+r][u] p.
%abbrev m   : cat = [k][a][p][m][r][m+r][u] m.
%abbrev r   : cat = [k][a][p][m][r][m+r][u] r.
%abbrev m+r : cat = [k][a][p][m][r][m+r][u] m+r.
%abbrev u   : cat = [k][a][p][m][r][m+r][u] u.
</twelf>

==== Terms and term constructors ====

We can now introduce the type <tt>tm</tt> of terms, which is indexed by <tt>cat</tt>, as well as the various term constructors:

<twelf import="ex3d">
tm  : cat -> type.

typ      : tm k.                               % type (the base kind)
pi_k     : tm a -> (tm r -> tm k) -> tm k.     % kind-level product

coer_pa  : tm p -> tm a.                       % type coercion
pi_a     : tm a -> (tm r -> tm a) -> tm a.     % type-level product

app_p    : tm p -> tm m -> tm p.               % atomic family application

coer_rm  : tm r -> tm m.                       % term coercion
lam_m    : (tm r -> tm m) -> tm m.             % term-level lambda abstraction

app_r    : tm r -> tm m -> tm r.               % atomic term application

canon    : tm m -> tm m+r.                     % left injection
atom     : tm r -> tm m+r.                     % right injection

*        : tm u.                               % unit
</twelf>

Here, we have used the standard [[higher-order abstract syntax]] representations of binders, which in this case always abstract over atomic term variables.  We have also included explicit coercions from atomic to canonical types and terms, since we really have disjoint syntactic categories here, instead of the usual overlapping ones, so some operation is needed to move from one category to another.

==== Main judgments ====

We come now to the main judgments, classification and substitution.  We haven't yet made any special use of the constraint domain&mdash;for everything above, we could have used an ordinary type&mdash;but this is where we will need it.  The reason is that, in order to state these judgments in a general enough way to include all of the usual stratified LF judgments, we will need to be able to track the functions that take a category to the category of objects that classifies it, as well as the function that takes a category to the category of objects that result from substitutions into it (recall the subtlety with M+R).  These are provided by the functions <tt>class</tt> and <tt>subst</tt>, respectively:

<twelf import="ex3d">
%abbrev class : cat -> cat = [X:cat] [k][a][p][m][r] [m+r][u]
                              X       u  u  k  a  a   a    u.

%abbrev subst : cat -> cat = [X:cat] [k][a][p][m][r] [m+r][u]
                              X       k  a  p  m  m+r m+r  u.

classj : tm X -> tm (class X) -> type.

substj : (tm r -> tm X) -> tm m -> tm (subst X) -> type.
</twelf>

Here, <tt>classj U V</tt> means that <tt>U</tt> is classified by (has type, has kind, has hyperkind) <tt>V</tt>, and <tt>substj ([x] E x) M E'</tt> means that <tt>E'</tt> is the result of substituting <tt>M</tt> for <tt>x</tt> in <tt>E</tt>.  Also, notice that the category index of each of these judgments is ''implicit'', so you won't see any categories mentioned in the rules that follow.

==== Rules for classification ====

We now give the encodings of the rules for classification.  For the purposes of this tutorial, it is not necessary that you study these rules in detail (unless they interest you!); just notice that Twelf is, in each case, able to infer the syntactic categories of all syntactic constituents of these rules by the using the term constructors involved, as well as the <tt>class</tt> function used to track the category of the classifier.

<twelf import="ex3d">
canon_kind_type : classj typ *.

canon_kind_pi   : classj (pi_k A2 [y] K y) *
                   <- classj A2 *
                   <- {y} classj y A2 -> classj (K y) *.

canon_fam_pi    : classj (pi_a A2 [y] A y) *
                   <- classj A2 *
                   <- ({y} classj y A2 -> classj (A y) *).

canon_fam_atom  : classj (coer_pa P) *
                   <- classj P typ.

atom_fam_app    : classj (app_p P1 M2) K
                  <- classj P1 (pi_k A2 [y] K1 y)
                  <- classj M2 A2
                  <- substj ([x] K1 x) M2 K.

canon_term_lam  : classj (lam_m [y] M y) (pi_a A2 [y] A y)
                   <- ({y} classj y A2 -> classj (M y) (A y)).

canon_term_atom : classj (coer_rm R) (coer_pa P)
                   <- classj R (coer_pa P).

% atom_term_var not necessary

atom_term_app   : classj (app_r R1 M2) A
                   <- classj R1 (pi_a A2 [y] A1 y)
                   <- classj M2 A2
                   <- substj ([x] A1 x) M2 A.

canon_atom_can  : classj (canon M) A
                   <- classj M A.

canon_atom_atom : classj (atom R) A
                   <- classj R A.

unit_unit       : classj * *.
</twelf>

==== Rules for substitution ====

Similarly, we present the rules for substitution.  Just notice here that categories are inferred in the same way as with classification, with the added subtlety of the disjoint union type returned by substitutions into atomic terms:

<twelf import="ex3d">
subst_k_type  : substj ([x] typ) M0 typ.

subst_k_pi    : substj ([x] pi_k (A x) [y] K x y) M0 (pi_k A' [y] K' y)
                 <- substj ([x] A x) M0 A'
                 <- {y} substj ([x] y) M0 (atom y)
                    -> substj ([x] K x y) M0 (K' y).

subst_a_p     : substj ([x] coer_pa (P x)) M0 (coer_pa P')
                 <- substj ([x] P x) M0 P'.

subst_a_pi    : substj ([x] pi_a (A2 x) [y] A x y) M0 (pi_a A2' [y] A' y)
                 <- substj ([x] A2 x) M0 A2'
                 <- {y} substj ([x] y) M0 (atom y)
                    -> substj ([x] A x y) M0 (A' y).

subst_p_app   : substj ([x] app_p (P x) (M x)) M0 (app_p P' M')
                 <- substj ([x] P x) M0 P'
                 <- substj ([x] M x) M0 M'.

subst_m_lam   : substj ([x] lam_m [y] M x y) M0 (lam_m [y] M' y)
                 <- {y} substj ([x] y) M0 (atom y)
                    -> substj ([x] M x y) M0 (M' y).

subst_m_rh  : substj ([x] coer_rm (R x)) M0 M'
               <- substj ([x] R x) M0 (canon M').

subst_m_r   : substj ([x] coer_rm (R x)) M0 (coer_rm R')
               <- substj ([x] R x) M0 (atom R').

subst_rh_var  : substj ([x] x) M0 (canon M0).

% subst_r_var not necessary

subst_rh_app : substj ([x] app_r (R1 x) (M2 x)) M0 (canon M'')
                <- substj ([x] R1 x) M0 (canon (lam_m [y] M' y))
                <- substj ([x] M2 x) M0 M2'
                <- substj ([y] M' y) M2' M''.

subst_r_app : substj ([x] app_r (R1 x) (M2 x)) M0 (atom (app_r R1' M2'))
                <- substj ([x] R1 x) M0 (atom R1')
                <- substj ([x] M2 x) M0 M2'.

subst_m+r_canon : substj ([x] canon (M x)) M0 (canon M')
                   <- substj ([x] M x) M0 M'.

subst_m+r_atom  : substj ([x] atom (P x)) M0 (atom P')
                   <- substj ([x] P x) M0 (atom P').

subst_unit_unit : substj ([x] *) M0 *.
</twelf>

==== Statement of the substitution theorem ====

We finish up this example with one more illustration of the unification afforded by our constraint domain: the statement of the substitution theorem for LF.  The theorem statement involves an implicit quantification over the syntactic category.  This means, in particular, that it isn't necessary to prove this by a mutual induction over the syntactic category, as is usually the case.  (Note, however, that because of the simplifications to LF that we have used for this presentation, it won't be possible to prove the theorem in this form.)

<twelf import="ex3d">
sub_thm : {T:tm r -> tm X} classj M0 A0
           -> ({x:tm r} classj x A0 -> classj (T x) (C x))
           -> substj ([x] C x) M0 C'
           %%---------------------------------------------
           -> {T':tm (subst X)}
              substj ([x] T x) M0 T'
           -> classj T' C'
           -> type.
</twelf>

<twelflink import="ex3d" check="true">See Twelf's output</twelflink> for all the code in this section.  Expansion of abbreviations is really hurting us here in this output, since <tt>nat</tt> expands to a type with seven arrows!

== Example 4: Lists of length ''n'' ==

All of the constraint domains that we have used so far have been finite.  For our last example, we will use an infinite constraint domain to implement one of the examples from the Twelf Manual that was used to illustrate the built-in constraint domain of rational numbers.  The example defines a dependent type of lists of items, where the index is the length of the list.  It then defines an append function, using the integer addition function to track the index of the result.  We can achieve that result here by using a Church encoding of the natural numbers:

<twelf import="start" export="ex4">
t : type.

%abbrev nat = (t -> t) -> t -> t.

%abbrev 0   = [s:t -> t] [z:t] z.
%abbrev s   = [n:nat] [s:t -> t] [z:t] s (n s z).
%abbrev +   = [n:nat] [m:nat] [s:t -> t] [z:t] n s (m s z).  %infix right 10 +.

item : type.
list : nat -> type.    % list N = item lists of length N

nil  : list 0.
cons : item -> list N -> list (s N).

append : list M -> list N -> list (M + N) -> type.

append_nil  : append nil L L.
append_cons : append (cons T L1) L2 (cons T L3)
               <- append L1 L2 L3.
</twelf>

It is interesting to note here that, in order to type-check the declarations for <tt>append_nil</tt> and <tt>append_cons</tt>, Twelf needs to prove for itself that the natural numbers defined above satisfy, respectively,

# 0 + <tt>X</tt> = <tt>X</tt>, for all <tt>X</tt> in <tt>nat</tt>, and
# <tt>X + Y = Z</tt> implies <tt>(s X) + Y = (s Z)</tt>, for all <tt>X</tt>, <tt>Y</tt>, and <tt>Z</tt> in <tt>nat</tt>.

These are statements&mdash;an equation and an implication between two equations&mdash;that are universally quantified over an infinite set, and are normally the kind of statement that is proved by induction on the natural numbers.  So, is Twelf secretly proving statements by induction during type-checking?  No, but that would be nice!  In fact, in the case of 1, if you apply the definition of <tt>+</tt> to <tt>0</tt> and a ''free variable'' <tt>X</tt> of type <tt>nat</tt> and then (beta-eta) normalize the result, you will see that it is equal (i.e., beta-eta equivalent) to <tt>X</tt>.  Similarly, if you apply the definitions of <tt>+</tt> and <tt>s</tt> to <tt>(s X) + Y</tt>, on the one hand, and <tt>s(X + Y)</tt> on the other, for free variables <tt>X</tt> and <tt>Y</tt> of type <tt>nat</tt>, you will see that they are again equal.  Thus, all Twelf needs in order to prove 1 and 2 above is beta-eta equivalence!

<twelflink import="ex4" check="true">See Twelf's output</twelflink>

== Conclusion ==

The techniques described in this tutorial fit into the general category of using indexing of types instead of predicates to make encodings more intrinsic.  Our so-called user-defined constraint domains were one variety of index type that we found to be useful in this regard, since they allow us to define "tracking functions" that would be impossible to define with a free representation.

However, there are still some issues that we have not yet examined.  For example,

* All of our domains were defined in terms of a type <tt>t</tt>, and we saw that it was necessary for the method that the type hierarchy over <tt>t</tt> (or at least the part of it that we are using) doesn't acquire any new canonical elements other than those that exist for all <tt>t</tt>, but we didn't talk about the best way to insure this in our developments.

* The examples we gave where certain ill-formed terms produced unresolved constraints when we tried to type-check them are in a kind of nether world between terms that type-check and terms that fail to type-check.  We would like to believe that the set of constraints produced on these examples by Twelf's type-checker are unresolvable, but there is no part of Twelf that can check that.

Nevertheless, these examples illustrate once again the power of LF as a representational framework.