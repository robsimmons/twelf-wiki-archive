'''Coverage checking''' is used to describe two analyses that Twelf needs to perform in order to verify [[totality assertions]]. 

The first analysis, for '''input coverage''', is performed by both <tt>[[%total]]</tt> and <tt>[[%covers]]</tt> declarations and resembles exhaustiveness checking in a language like ML. 

The second analysis, '''output coverage''', is performed only by <tt>[[%total]]</tt> to ensure that the outputs of subgoals are sufficiently general.  Output coverage consists of a check for incorrect constant pattern-matching and a check for [[output freeness]] violations.  

We will use the common example of [[natural numbers]] for this example:

<twelf import="nat">
nat: type.

z: nat.
s: nat -> nat.

</twelf>

== Input coverage ==

Input coverage is similar to exhaustiveness checking in ML, in that it ensures that your relation is prepared to accept any input that is thrown at it. We would run afoul of the input coverage checker if we tried to run a <tt>%covers</tt> or <tt>%total</tt> declaration on the relation <tt>half</tt> that halves a natural number but is undefined on odd numbers. Another example of input coverage checking can be seen at the page on <tt>[[%covers]]</tt>.

<twelf import="nat" export="one">
half : nat -> nat -> type.
%mode half +N1 -N2. 

half/z : half z z.
half/s : half (s (s N)) (s M)
          <- half N M.

%worlds () (half _ _).
</twelf>

<twelf import="one" check=decl discard=true>
%total N (half N _).
</twelf>

== Output coverage ==

Output coverage ensures that your relation is prepared to deal with any possible term that it might be output by a [[subgoal]]. Output coverage does not have quite so obvious a counterpart in functional languages like ML; the reason is that Twelf differentiates between case-analyzing outputs and case-analyzing inputs, whereas in ML there is just one case construct.  

=== Output constant pattern-matching ===

Say we attempted to fix <tt>half</tt> by defining <tt>half</tt> of 1 to be 0 by adding the case <tt>half/1</tt> shown below:

<twelf import="nat" export="two">
half : nat -> nat -> type.
%mode half +N1 -N2.

half/z : half z z.
half/1 : half (s z) z
          <- half z z.
half/s : half (s (s N)) (s M)
          <- half N M.

%worlds () (half _ _).
</twelf>

For pedagogical purposes, we have given the constant <tt>half/1</tt> an unnecessary extra premise that <tt>half z z </tt>.  Thus, the case <tt>half/1</tt> expects the "output" of the subgoal <tt>&lt;- half z z</tt> to be <tt>z</tt>; inspecting the program, this is a true fact, but Twelf is not capable of verifying this and complains accordingly.

<twelf import="two" check=decl>
%total N (half N _).
</twelf>

In this case, we can fix the problem in one of two ways. First, we can remove the subgoal entirely:

<twelf discard=true>
half/1 : half (s z) z.
</twelf>

Alternatively, we can rewrite the subgoal, leaving the output a free variable:

<twelf discard=true>
half/1 : half (s z) N
          <- half z N.
</twelf>

In many circumstances when the output coverage checker rejects a totality assertion that should, in fact, be true, a standard technique [[output factoring]] can deal with the problem.

=== Output freeness === 

The other part of output coverage checking is [[output freeness]] checking; see its page for a definition and examples.

== See also ==

* {{guide|section=52|chapter=9|title=Coverage}}
* [[Output freeness]]
* [[Debugging coverage errors]]
* [[Output factoring]]