::''You should not attach any particular significance to the fact that Twelf returns OK (or raises an uncaught exception, or starts playing the theme music to M*A*S*H...) on a signature that mentions a constraint domain and also tries to prove coverage or totality metatheorems.'' - Kevin Watkins

It is tempting to try to use [[constraint domains]] to do seemingly innocent helpful things. However, at the present time, constraint domains should be seen as completely incompatible with features that Twelf uses to prove [[metatheorems]], such as <tt>[[%mode]]</tt>, <tt>[[%covers]]</tt>, and <tt>[[%total]]</tt>. 

One particular way that this error happens is when users attempt to formalize a language with exceptions, and want to index their language with strings.

<twelf discard=true>
%use equality/strings. 

exp : type.
error : string -> exp.
</twelf>

This should be avoided at all costs in favor of explicitly defining a type of error messages ''within'' the signature.

<twelf discard=true>
errormessage : type.

bad-value : errormessage.
type-error : errormessage. 
% ...etc...

exp : type.
error : errormessage -> exp.
</twelf>

== Example: apparent unsoundness with constraint domains ==

The essential problem is that the interaction between constraint domains and the Twelf
coverage checker is not well defined. Take the following example:

<twelf>
%use word32. 

exp : type.

x : word32 -> exp.
y : exp.
z : exp.

q : exp -> exp -> type.
%mode q +A1 -A2. 

q/y : q y y.

q/z : q z z.
</twelf>

Twelf appears to allow us to prove that, for any input in the first position, <tt>q</tt> will always run
successfully and return an output in the second position.

<twelf name="fork" check=decl>
%worlds () (q _ _). 
%total T (q T _).  
</twelf>

However, Twelf's assertion of totality here is completely bogus, which we can see if we try to run
q as a logic program with <tt>(x 5)</tt> in the first position.

<twelf include=fork check=decl>
%solve _ : q (x 5) _.
</twelf>

=== Analysis ===

{{needs|copy editing}}

One way of looking at the problem above is that the above proof isn't quite a proof of what it seems to be a proof of. The <tt>[[%worlds]]</tt> statement for the above theorem is empty, which means that one way to state the theorem theorem is "if the only things that exist in the things that were explicitly defined in the signature." This is a closed-world assumption. Because the identifier <tt>5</tt> never was explicitly defined in the signature, but was implicitly introduced by the <tt>%use word32</tt> declaration, then the metatheorem above is true - if we're not allowed use any object of type <tt>word32</tt>, the we can't feed
<tt>q</tt> any input that will allow it to fail.

If we modify our worlds declaration to specify that there may be some <tt>word32</tt>s around&mdash;because there ''are'' some 
<tt>word32</tt>s around, there are <math>2^{32}</math> of them!&mdash;then the theorem fails as it should.

<twelf check=decl>
%block some-word32s : block {x: word32}.

%worlds (some-word32s) (q _ _).
%total T (q T _).
</twelf>

However, as there is no account for exactly what <tt>%use</tt> ''does'' to the signature, it is very difficult to be certain, when we are using constraint domains, that we have proven the theorems we think we have proven. This is precisely
why constraint domains and metatheorem declarations like <tt>%covers</tt> and <tt>%total</tt> should never be used in the same signature.