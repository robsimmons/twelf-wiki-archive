The '''<tt>%mode</tt>''' declaration specifies the way in which a relation is intended to be used, by specifying those arguments that are ''inputs'' or ''outputs''. Twelf then checks that all constants defining the relation respect the mode specified (are "well-moded").

The <tt>%mode</tt> declaration is an important part of the specification of a [[totality assertion]]. In the "for all/exists" statement about a relation, the "for all" terms are the input arguments to the relation, and the "exists" terms are its outputs.

== Syntax ==

A mode declaration for the family ''id'' with ''n'' arguments usually takes the following ("short") form:

<code>%mode id a1 ... an.</code>

Each argument <tt>ai</tt> is <tt>+</tt>''ID'', <tt>-</tt>''ID'', or <tt>*</tt>''ID'' for some distinct identifier ''ID''. <tt>+</tt> indicates that the argument is an input, <tt>-</tt> that it is an output, and <tt>*</tt> that the argument is unrestricted. (The <tt>*</tt> mode is rarely used.) For example, a natural mode for the <tt>plus</tt> relation on [[natural numbers]] is:

<twelf discard=true>
plus : nat -> nat -> nat -> type.
%mode plus +N1 +N2 -N3.
</twelf>

The names <tt>N1</tt>..<tt>N3</tt> are arbitrary, but each must be distinct.

=== Full form ===

The short mode declaration above only specifies the mode for explicit parameters; the modes for implicit parameters are then assigned automatically. Occasionally, one needs to use a "full" form to specify the modes of implicit parameters:

<twelf discard=true>
%mode p1 ... pm  term.
</twelf>

Each parameter <tt>pi</tt> is <tt>+{</tt>''ID''<tt> : term}</tt>, <tt>-{</tt>''ID''<tt> : term}</tt>, or <tt>*{</tt>''ID''<tt> : term}</tt> for distinct identifiers ''ID''. Terms may refer to the variables bound in the previous parameters. For example, the full mode declaration corresponding to the short one above would be:

<twelf discard=true>
plus : nat -> nat -> nat -> type.
%mode +{N1:nat} +{N2:nat} -{N3:nat} plus N1 N2 N3.
</twelf>

{{needs|an example where the full mode specification is actually needed}}

== Mode checking ==

===Basic mode checking errors===

Say we give a definition of the natural numbers with addition in line with the descriptions above:

<twelf>
nat: type.
z: nat.
s: nat -> nat.

plus: nat -> nat -> nat -> type.
%mode plus +N1 +N2 -N3.

pz: plus z N N.
ps: plus (s N1) N2 (s N3)
     <- plus N1 N2 N3.
</twelf>

The input of plus is the first and second positions, and the output of plus is the third position, which means that for any [[ground]] objects in the first and second positions, there must only be a ground object in the third position. If one of the output arguments is not forced to be a ground, which would be the case if the output of plus no longer matched the output of the [[subgoal]], then 

<twelf check=decl discard=true>
px: plus (s N1) N2 (s N)
     <- plus N1 N2 N3.
</twelf>

Another problem occurs when the ''inputs'' to a subgoal are not known to be ground, which would happen if we mis-named one of the inputs to the subgoal.

<twelf check=decl discard=true>
py: plus (s N1) N2 (s N3)
     <- plus N N2 N3.
</twelf>

===Ordering subgoals===

Mode checking considers subgoals ''in order'', i.e. from top to bottom when the subgoals are written out in the standard style using backwards arrows. The order of subgoals matters very much for mode checking. Say we have an identity function that maps inputs (the first position) to outputs (the second position).

<twelf>
id: nat -> nat -> type.
%mode id +N1 -N2.

id/refl: id N N.
</twelf>

The rule <tt>ps'</tt> below passes the mode checker, because the call to <tt>id</tt> takes the ground argument <tt>N1</tt> and creates a ground output <tt>N1'</tt>, which is then used in the derivation for <tt>plus N1' N2 N3</tt>.

<twelf check=decl discard=true>
ps': plus (s N1) N2 (s N3)
      <- id N1 N1'
      <- plus N1' N2 N3.
</twelf>

However, if we reverse the order of the two subgoals, even though the result is logically equivalent, Twelf considers <tt>plus N1' N2 N3</tt> before <tt>id N1 N1'</tt>, and so does consider <tt>N1'</tt> to be ground when it encounters it; thus, complaining accordingly: 

<twelf check=decl discard=true>
ps': plus (s N1) N2 (s N3)
      <- plus N1' N2 N3
      <- id N1 N1'.
</twelf>

{{needs|a more complete description of more interesting mode checking problems}}

== See also ==
* {{guide|chapter=7|section=41|title=Modes}}
* {{proving|title=Proving totality assertions about the natural numbers}}, particulary the section on [[Proving metatheorems:Proving_totality_assertions_about_the_natural_numbers#Mode|Mode]].


{{keyword|key=mode}}