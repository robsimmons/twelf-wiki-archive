A '''<tt>%name</tt>''' declaration allows us to direct Twelf to name unnamed variables in a certain way, which can make it much easier to understand Twelf's output. Using a <tt>%name</tt> declaration is never required, but it often makes the task of proving [[metatheorem]]s significantly easier. More information can be found in the section on {{guide|chapter=3|section=5|title=name preferences}}.

Two examples show the use of <tt>%name</tt> - the first shows its use to give a default name for universally quantified variables, and the second example shows its use to give a default name for both universally quantified variables and bound variables.

== Example 1: Natural numbers ==

We start with a standard presentation of unary numbers and addition:

<twelf>
nat : type.

z : nat.
s : nat -> nat.



plus : nat -> nat -> nat -> type.

plus/z : plus z N N.

plus/s : plus (s N1) N2 (s N3) <- plus N1 N2 N3.
</twelf>


If we give Twelf a derivation, but do not give it information about the type of that derivation, it will infer the type. In order to return the most general possible type, Twelf considers the second type to be an "anonymous" (or [[universally quantified]]) variable. If Twelf is given no other information, it will automatically name all anonymous variables <tt>X1</tt>, <tt>X2</tt>, etc...

<twelf check="decl">
_ = plus/s (plus/s plus/z).
</twelf>


These type reconstructions can become rather complicated, particularly when we are dealing with metatheorems that have multiple types. A good way to deal with this complexity is by giving Twelf a different prefix for anonymous variables of different types using the <tt>%name</tt> declaration.

<twelf>
%name nat N.
</twelf>


This identifier ''must'' start with an uppercase letter, and often only a single uppercase letter suffices; however, any identifier starting with an uppercase letter works. Given this information, Twelf will change the prefix of anonymous variables from <tt>X</tt> to whatever was defined in the <tt>%name</tt> declaration.

<twelf check="decl">
_ = plus/s (plus/s plus/z).
</twelf>

== Example 2: Call-by-name lambda calculus ==

We can also use the <tt>%name</tt> declaration to define the default name for bound variables. 

<twelf import="lam">
exp : type. 

lam : (exp -> exp) -> exp.
app : exp -> exp -> exp.



step : exp -> exp -> type.

step/app : step (app E1 E2) (app E1' E2) <- step E1 E1'.

step/appabs : step (app (lam E1) E2) (E1 E2).
</twelf>


The default prefix for universally quantified variables is again <tt>X</tt>. Also, observe that the default prefix for bound variables is <tt>x</tt>.

<twelf import="lam" check=decl>
_ = (step/app (step/app step/appabs)).
</twelf>


This <tt>%name</tt> declaration causes no output from Twelf, but it changes the prefixes of universally quantified and bound variables to <tt>E</tt> and <tt>e</tt>, respectively.

<twelf import="lam" check=decl>
%name exp E e.

_ = (step/app (step/app step/appabs)).
</twelf>

== See also ==
* {{guide|chapter=3|section=5|title=Name preferences}}

{{keyword|key=name}}